const count_leaves = make_machine(
    list("tree", "continue", "val", "var"),
    list(
        list("+", (a, b) => a + b),
        list("is_null", is_null),
        list("is_pair", is_pair),
        list("head", head),
        list("tail", tail),
    ),
    list(
        assign("continue", label("count_leaves_done")),
        assign("val", constant(0)),
        "tree_loop",
            test(list(op("is_null"), reg("tree"))),
            branch(label("null_tree")),
            test(list(op("is_pair"), reg("tree"))),
            branch(label("left_tree")),
            assign("val", constant(1)),
            go_to(register("continue")),
        "left_tree",
            save("tree"),
            save("continue"),
            assign("continue", label("right_tree")),
            assign("tree", list(op("head"), reg("tree"))),
            go_to(label("tree_loop")),
        "right_tree",
            restore("continue"),
            restore("tree"),
            save("continue"),
            save("val"),
            assign("continue", label("after_tree")),
            assign("tree", list(op("tail"), reg("tree"))),
            go_to(label("tree_loop")),
        "after_tree",
            assign("var", reg("val")),
            restore("val"),
            restore("continue"),
            assign("val", list(op("+"), reg("var"), reg("val"))),
            go_to(register("continue")),
        "null_tree",
            assign("val", constant(0)),
            go_to(register("continue")),
        "count_leaves_done"
    )
);

const count_leaves_counter = make_machine(
    list("list", "continue", "val"),
    list(
        list("+", (a, b) => a + b),
        list("is_null", is_null),
        list("is_pair", is_pair),
        list("head", head),
        list("tail", tail),
    ),
    list(
        "start",
            assign("val", constant(0)),
            assign("continue", label("done")),
            save("continue"),
            assign("continue", label("tail_loop")),
        "count_loop",
            test(list(op("is_pair"), reg("list"))),
            branch(label("pair")),
            test(list(op("is_null"), reg("list"))),
            branch(label("null")),
            assign("val", list(op("+"), reg("val"), constant(1))),
            restore("continue"),
            go_to(register("continue")),
        "tail_loop",
            restore("list"),
            assign("list", list(op("tail"), reg("list"))),
            go_to(label("count_loop")),
        "pair",
            save("list"),
            save("continue"),
            assign("list", list(op("head"), reg("list"))),
            go_to(label("count_loop")),
        "null",
            restore("continue"),
            go_to(register("continue")),
        "done"
    )
);
