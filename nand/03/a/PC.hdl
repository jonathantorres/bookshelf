// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], inc, load, reset;
    OUT out[16];

    PARTS:
    Or(a=reset, b=load, out=rl);
    Or(a=rl, b=inc, out=lr);
    Inc16(in=rout, out=iout);
    Mux16(a=rout, b=iout, sel=inc, out=m1);
    Mux16(a=m1, b=in, sel=load, out=m2);
    Mux16(a=m2, b=false, sel=reset, out=mout);
    Register(in=mout, load=lr, out=rout, out=out);
}
