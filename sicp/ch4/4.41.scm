(define (multiple-dwellings)
  (define (solution? baker cooper fletcher miller smith)
    (and (not (= baker 5))
         (not (= cooper 1))
         (not (= fletcher 5))
         (not (= fletcher 1))
         (> miller cooper)
         (not (= (abs (- smith fletcher)) 1))
         (not (= (abs (- fletcher cooper)) 1))))
  (define (combine-lists a b)
    (if (null? a)
      '()
      (cons (list (car a) (car b))
            (combine-lists (cdr a) (cdr b)))))
  (map (lambda (floors)
         (combine-lists '(baker cooper fletcher miller smith) floors))
       (filter (lambda (floors)
                 (apply solution? floors))
               (permute 5))))

(define (permute n)
  (define (insert n items)
    (if (null? items)
        (list (cons n '()))
        (cons (cons n items)
              (map (lambda (rest) (cons (car items) rest))
                   (insert n (cdr items))))))
  (if (= n 1)
      '((1))
      (flatmap (lambda (items) (insert n items))
                (permute (- n 1)))))

(define (flatmap proc items)
  (if (null? items)
      '()
      (append (proc (car items))
              (flatmap proc (cdr items)))))

(display (multiple-dwellings))
(newline)
