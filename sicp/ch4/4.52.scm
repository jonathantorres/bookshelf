(include "amb.scm")

(define (if-fail? exp) (tagged-list? exp 'if-fail))
(define (if-fail-cond exp) (cadr exp))
(define (if-fail-alt exp) (caddr exp))

(define (analyze-if-fail exp)
  (let ((condp (analyze (if-fail-cond exp)))
        (altp (analyze (if-fail-alt exp))))
    (lambda (env ok err)
      (condp env ok
             (lambda ()
               (altp env ok err))))))

(define (analyze exp)
  (cond ((self-evaluating? exp)
         (analyze-self-evaluating exp))
        ((quoted? exp) (analyze-quoted exp))
        ((variable? exp) (analyze-variable exp))
        ((assignment? exp) (analyze-assignment exp))
        ((definition? exp) (analyze-definition exp))
        ((if? exp) (analyze-if exp))
        ((if-fail? exp) (analyze-if-fail exp))
        ((lambda? exp) (analyze-lambda exp))
        ((begin? exp) (analyze-sequence (begin-actions exp)))
        ((cond? exp) (analyze (cond->if exp)))
        ((let? exp) (analyze (let->combination exp)))
        ((amb? exp) (analyze-amb exp))
        ((application? exp) (analyze-application exp))
        (else
         (error "Unknown expression type -- ANALYZE" exp))))
(define prog
  '((define (require p)
      (if (not p) (amb)))
    (define (even? x)
      (= (remainder x 2) 0))
    (define (an-element-of items)
      (require (not (null? items)))
      (amb (car items) (an-element-of (cdr items))))))

(define (run)
  (define env (setup-environment))
  (for-each (lambda (p)
              (ambeval p env
                       (lambda (value fail) (cons value fail))
                       (lambda () 'done)))
            prog)
  (define exp '(if-fail (let ((x (an-element-of '(1 3 5 8))))
                          (require (even? x))
                          x)
                        'all-odd))
  (display (ambeval exp env
                    (lambda (value fail) value)
                    (lambda () 'done))))
(run)
(newline)
